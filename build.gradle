import groovy.text.SimpleTemplateEngine

import java.util.stream.Collectors

plugins {
    id 'jacoco'
    id "org.spongepowered.gradle.sponge.dev" version "2.0.2"
    id "math-templates"
    id "net.kyori.indra" version "2.1.1"
    id "net.kyori.indra.publishing.sonatype" version "2.1.1"
    id "net.ltgt.errorprone" version "2.0.2"
}

// -- General setup -- //

group = "org.spongepowered"
version = "2.0.2-SNAPSHOT"
description = "Immutable math library for Java with a focus on games and computer graphics."

repositories {
    maven {
        url = "https://repo.spongepowered.org/repository/maven-public/"
        name = "sponge"
    }
}

// Metadata

spongeConvention {
    repository("math") {
        ci(true)
        publishing(true)
    }

    mitLicense()
    licenseParameters {
        name = "Math"
        organization = project.organization
        url = project.url
    }

    sharedManifest {
        attributes "Specification-Title": project.name,
                "Specification-Vendor": "SpongePowered - https://spongepowered.org"
    }
}

def floatData = file("src/templateData/float.yaml")
def intData = file("src/templateData/integer.yaml")
def licenseText = objects.property(String)
licenseText.set(provider {
    def properties = [*: license.ext.properties]
    def template = new SimpleTemplateEngine().createTemplate(file("HEADER.txt")).make(properties)
    def text = template.toString()
    def lineEnding = license.lineEnding.get()
    Arrays.stream(text.split("\r?\n"))
            .map { if (it.isEmpty()) { " *" } else { " * $it" } }
            .collect(Collectors.joining(lineEnding, "/*$lineEnding", "$lineEnding */"))
})
licenseText.finalizeValueOnRead()

sourceSets {
    main {
        templates.templateSets {
            register("float") {
                dataFiles.from(floatData)
                variants("float", "double")
            }
            register("integer") {
                dataFiles.from(intData)
                variants("int", "long")
            }
            // no variants
            register("floatCommon") {
                dataFiles.from(floatData)
            }
        }

        // Add a module-info
        multirelease {
            alternateVersions(9)
            moduleName("org.spongepowered.math")
            requireAllPackagesExported()
            applyToJavadoc(true)
        }
    }
    test {
        templates.templateSets {
            register("integer") {
                dataFiles.from(intData)
                variants("int", "long")
            }
            register("float") {
                dataFiles.from(floatData)
                variants("float", "double")
            }
        }
    }
    configureEach {
        templates.templateSets.configureEach {
            header.set(licenseText)
        }
    }
}


dependencies {
    compileOnlyApi("com.google.errorprone:error_prone_annotations:$errorproneVersion")
    errorprone("com.google.errorprone:error_prone_core:$errorproneVersion")

    testImplementation(platform("org.junit:junit-bom:$junitVersion"))
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

jar {
    from(rootProject.file("LICENSE.txt")) {
        rename { "LICENSE-spongepowered-math.txt" }
    }
}

tasks.withType(JavaCompile.class) {
    options.compilerArgs << "-Xlint:-cast" // skip cast warnings, the generated source is most likely just overly safe.
}

afterEvaluate {
    javadoc {
        options {
            addBooleanOption("-no-module-directories", false)
        }
    }
}

plugins.withId 'eclipse', {
    eclipse {
        classpath.file.whenMerged { path ->
            path.entries.each {
                if (it instanceof org.gradle.plugins.ide.eclipse.model.SourceFolder) {
                    def extraReads = ['org.junit.jupiter.api']
                        .collect { mod -> "org.spongepowered.math=$mod" }
                        .join(':')
                    it.entryAttributes['add-reads'] = extraReads
                }
            }
        }
    }
}

// -- Publishing -- //
indra {
    javaVersions {
        testWith(8, 11, 17)
    }
    configurePublications {
            pom {
                name = "Math"
                inceptionYear = "2013"

                developers {
                    developer {
                        id = "DDoS"
                        name = "Aleksi Sapon"
                        email = "qctechs@gmail.com"
                    }
                    developer {
                        id = "kitskub"
                        name = "Jack Huey"
                        email = "kitskub@gmail.com"
                    }
                    developer {
                        id = "Wolf480pl"
                        name = "Wolf480pl"
                        email = "wolf480@interia.pl"
                    }
                    developer {
                        id = "lukespragg"
                        name = "Luke Spragg"
                        email = "the@wulf.im"
                    }
                }
            }
        }
    }
